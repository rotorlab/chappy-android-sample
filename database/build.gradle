apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

version = "1.3.0"

android {

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 27
        versionCode 7
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.0.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.code.gson:gson:2.8.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    implementation 'com.efraespada:jsondiff:1.1.0'
    implementation 'org.skyscreamer:jsonassert:1.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.findbugs:jsr305:2.0.1'

    //implementation 'io.lettuce:lettuce-core:5.0.0.RELEASE'
    implementation "io.projectreactor:reactor-core:3.1.4.RELEASE"
    implementation 'org.latencyutils:LatencyUtils:2.0.3'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex:rxjava-reactive-streams:1.2.1'
    implementation 'org.springframework:spring-beans:5.0.4.RELEASE'
    implementation 'io.netty:netty-all:4.1.21.Final'
    implementation 'javax.enterprise:cdi-api:2.0'
    implementation 'org.apache.commons:commons-pool2:2.0'
    implementation 'org.yaml:snakeyaml:1.19'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    //noinspection GradleDependency
    implementation 'com.google.guava:guava:23.0-android'
    //noinspection GradleDependency
    implementation "com.stringcare:library:$stringcare_version"
    implementation ('io.socket:socket.io-client:1.0.0') {
        exclude group: 'org.json', module: 'json'
    }
}

def siteUrl = 'https://github.com/flamebase/flamebase-database-android'
def gitUrl = 'https://github.com/flamebase/flamebase-database-android.git'

group = "com.flamebase"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Real time JSON database for android client'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'efraespada'
                        name 'efraespada'
                        email 'efraespada@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "DatabaseAndroid"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}